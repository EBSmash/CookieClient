buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'impactdevelopment-repo'
            url = 'https://impactdevelopment.github.io/maven/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath "org.slf4j:slf4j-simple:1.6.1"


    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.03"
group = "me.bebeli555.cookieclient"
archivesBaseName = "CookieClient"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('src/main/resources/cookieclient_at.cfg')
    
    runs {
        client {
            workingDirectory project.file('run')
            
            property 'fml.coreMods.load', 'me.bebeli555.cookieclient.mixin.MixinLoader'
            property 'mixin.env.disableRefMap', 'true'
            
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = 'swt-repo'
        url = "https://maven-eclipse.github.io/maven"
    }
    
    mavenCentral()
    jcenter()
}

configurations {
    jarLibs
    all {
        resolutionStrategy {
            force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
        }
    }
}

//The ZeroMemes alpine isnt included here because some clients like impact use the newer version of it and then it crashes
dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
    
    implementation "com.github.cabaletta:baritone:1.2.14"
    jarLibs "cabaletta:baritone-api:1.2"

    jarLibs "net.jodah:typetools:0.6.2"

    jarLibs('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'commons-io'
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'launchwrapper'
        exclude module: 'log4j-core'
    }

    annotationProcessor('org.spongepowered:mixin:0.8.2:processor') {
        exclude module: 'gson'
    }
    
    implementation configurations.jarLibs
}

processResources {
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes(
                'MixinConfigs': 'mixins.cookieclient.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'me.bebeli555.cookieclient.mixin.MixinLoader',
                'ForceLoadAsMod': 'true',
                'FMLAT': 'cookieclient_at.cfg'
        )
    }
    
    from {
        configurations.jarLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

mixin {
    defaultObfuscationEnv 'searge'
    add sourceSets.main, "mixins.cookieclient.refmap.json"
}
